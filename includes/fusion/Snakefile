from rattle import Run

RUN = Run(config)


localrules: star_fusion_cp fusioncatcher_cp plot_cp

rule star_fusion:
    input:
        fq1=RUN.output("{sample}/{sample}-R1.fq.gz"),
        fq2=RUN.output("{sample}/{sample}-R2.fq.gz"),
        lib=RUN.settings["genome_star_fusion_lib"]
    output:
        dir=RUN.output("{sample}/fusion/star-fusion"),
        txt=RUN.output("{sample}/fusion/star-fusion/star-fusion.fusion_candidates.final")
    threads: 8
    conda: srcdir("envs/star_fusion.yml")
    shell:
        "STAR-Fusion --genome_lib_dir {input.lib}"
        " --output_dir {output.dir}"
        " --left_fq {input.fq1} --right_fq {input.fq2}"

rule star_fusion_cp:
    input:
        txt=RUN.output("{sample}/fusion/star-fusion/star-fusion.fusion_candidates.final"),
    output:
        txt=RUN.output("{sample}/fusion/{sample}.star-fusion"),
    shell:
        "cp {input.txt} {output.txt}"

rule fusioncatcher:
    input:
        fq1=RUN.output("{sample}/{sample}-R1.fq.gz"),
        fq2=RUN.output("{sample}/{sample}-R2.fq.gz"),
        exe=RUN.settings["fusioncatcher_exe"]
    output:
        dir=RUN.output("{sample}/fusion/fusioncatcher"),
        txt=RUN.output("{sample}/fusion/fusioncatcher/final-list_candidate-fusion-genes.txt")
    threads: 8
    shell:
        "(rm -rf {output.dir}"
        " && {input.exe} --threads {threads} -i {input.fq1},{input.fq2} -o {output.dir})"
        " || test -f {output.txt}"

rule fusioncatcher_cp:
    input:
        txt=RUN.output("{sample}/fusion/fusioncatcher/final-list_candidate-fusion-genes.txt"),
    output:
        txt=RUN.output("{sample}/fusion/{sample}.fusioncatcher"),
    shell:
        "cp {input.txt} {output.txt}"

rule plot_fusion:
    input:
        txt=RUN.output("{sample}/fusion/{sample}.{ext}"),
    output:
        svg=RUN.output("{sample}/fusion/{sample}.{ext}-circos/fsnviz.svg"),
    conda: srcdir("envs/plot_fusion.yml")
    shell:
        "PERL5LIB=\"\" fsnviz --out-dir `dirname {output.svg}` -k human.hg38 {wildcards.ext} {input.txt}"

rule plot_cp:
    input:
        svg=RUN.output("{sample}/fusion/{sample}.{ext}-circos/fsnviz.svg"),
    output:
        svg=RUN.output("{sample}/fusion/{sample}.{ext}.svg"),
    shell:
        "cp {input.svg} {output.svg}"
