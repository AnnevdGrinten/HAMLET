from os import path

# We need the QC-seq output
include: "../qc-seq/Snakefile"

localrules: combine_plots, fusioncatcher_cp, intersect_fusions, plot_cp, plot_isect, plot_sf star_fusion_cp, subset_sf


containers = {
    "debian": "docker://debian:buster-slim",
    "fsnviz": "docker://quay.io/biocontainers/fsnviz:0.3.0--py_3",
    "fuma": "docker://quay.io/biocontainers/fuma:3.0.5--py27h24bf2e0_1",
    "fusioncatcher": "docker://quay.io/biocontainers/fusioncatcher:1.20--2",
    "star-fusion": "docker://quay.io/biocontainers/star-fusion:1.6.0--1",
    "hamlet-scripts": "docker://quay.io/redmar_van_den_berg/hamlet-scripts:0.2"
}

settings=config["settings"]

# Set the default settings
def set_default(key, value):
    """ Set default value for settings """
    if key not in settings:
        settings[key] = value

set_default("sf_subset_script", srcdir(path.join("scripts", "subset_sf.py")))
set_default("plot_combined_script", srcdir(path.join("scripts", "combine_svgs.py")))

rule all_fusion:
    input:
        star_predictions=expand("{sample}/fusion/star-fusion/star-fusion.fusion_predictions.tsv", sample=config["samples"]),
        star_final=expand("{sample}/fusion/{sample}.star-fusion", sample=config["samples"]),
        fusion_catcher=expand("{sample}/fusion/{sample}.fusioncatcher", sample=config["samples"]),
        fuma=expand("{sample}/fusion/{sample}.fuma", sample=config["samples"]),
        subset=expand("{sample}/fusion/{sample}.sf-isect", sample=config["samples"]),
        is_svg=expand("{sample}/fusion/{sample}.sf-isect-circos/fsnviz.svg", sample=config["samples"]),
        is_png=expand("{sample}/fusion/{sample}.sf-isect-circos/fsnviz.png", sample=config["samples"]),
        sf_svg=expand("{sample}/fusion/{sample}.star-fusion-circos/fsnviz.svg", sample=config["samples"]),
        sf_png=expand("{sample}/fusion/{sample}.star-fusion-circos/fsnviz.png", sample=config["samples"]),
        fc_svg=expand("{sample}/fusion/{sample}.fusioncatcher-circos/fsnviz.svg", sample=config["samples"]),
        fc_png=expand("{sample}/fusion/{sample}.fusioncatcher-circos/fsnviz.png", sample=config["samples"]),
        combined_svg=expand("{sample}/fusion/{sample}.fusions-combined.svg", sample=config["samples"]),

rule star_fusion:
    input:
        fq1="{sample}/{sample}-R1.fq.gz",
        fq2="{sample}/{sample}-R2.fq.gz",
        lib=settings["genome_star_fusion_lib"]
    params:
        folder="{sample}/fusion/star-fusion"
    output:
        txt="{sample}/fusion/star-fusion/star-fusion.fusion_predictions.tsv"
    threads: 8
    singularity: containers["star-fusion"]
    shell:
        "(rm -rf {params.folder}"
        " && STAR-Fusion --genome_lib_dir {input.lib}"
        "   --output_dir {params.folder}"
        "   --left_fq {input.fq1} --right_fq {input.fq2})"

rule star_fusion_cp:
    input:
        txt="{sample}/fusion/star-fusion/star-fusion.fusion_predictions.tsv",
    output:
        txt="{sample}/fusion/{sample}.star-fusion",
    singularity: containers["debian"]
    shell:
        "cp {input.txt} {output.txt}"

rule plot_sf:
    input:
        txt="{sample}/fusion/{sample}.star-fusion",
    output:
        svg="{sample}/fusion/{sample}.star-fusion-circos/fsnviz.svg",
        png="{sample}/fusion/{sample}.star-fusion-circos/fsnviz.png",
    singularity: containers["fsnviz"]
    shell:
        "PERL5LIB=\"\" fsnviz --out-dir `dirname {output.svg}` --png"
        " -k human.hg38 star-fusion {input.txt}"

rule plot_cp:
    input:
        svg="{sample}/fusion/{sample}.{ext}-circos/fsnviz.svg",
    output:
        svg="{sample}/fusion/{sample}.{ext}.svg",
    wildcard_constraints:
        ext="star-fusion|sf-isect|fusioncatcher",
    singularity: containers["debian"]
    shell:
        "cp {input.svg} {output.svg}"

rule fusioncatcher:
    input:
        fq1="{sample}/{sample}-R1.raw.fq.gz",
        fq2="{sample}/{sample}-R2.raw.fq.gz",
    params:
        data=settings["fusioncatcher_data"]
    output:
        txt="{sample}/fusion/fusioncatcher/final-list_candidate-fusion-genes.txt"
    threads: 8
    singularity: containers["fusioncatcher"]
    shell:
        " dir=`dirname {output.txt}` "
        " && fusioncatcher.py --threads {threads} -i {input.fq1},{input.fq2} -o ${{dir}}"
        " --data {params.data} || test -f {output.txt}"

rule fusioncatcher_cp:
    input:
        txt="{sample}/fusion/fusioncatcher/final-list_candidate-fusion-genes.txt",
    output:
        txt="{sample}/fusion/{sample}.fusioncatcher",
    singularity: containers["debian"]
    shell:
        "cp {input.txt} {output.txt}"

rule plot_fc:
    input:
        txt="{sample}/fusion/{sample}.fusioncatcher",
    output:
        svg="{sample}/fusion/{sample}.fusioncatcher-circos/fsnviz.svg",
        png="{sample}/fusion/{sample}.fusioncatcher-circos/fsnviz.png",
    singularity: containers["fsnviz"]
    shell:
        "PERL5LIB=\"\" fsnviz --out-dir `dirname {output.svg}` --png"
        " -k human.hg38 fusioncatcher {input.txt}"

rule intersect_fusions:
    input:
        sf="{sample}/fusion/{sample}.star-fusion",
        fc="{sample}/fusion/{sample}.fusioncatcher",
        bed=settings["transcripts_bed"],
    output:
        txt="{sample}/fusion/{sample}.fuma",
    singularity: containers["fuma"]
    shell:
        "fuma -a hg38:{input.bed} -s"
        " fc-{wildcards.sample}:fusion-catcher_final:{input.fc}"
        " sf-{wildcards.sample}:star-fusion_final:{input.sf}"
        " -l \"fc-{wildcards.sample}:hg38\" \"sf-{wildcards.sample}:hg38\""
        " -f extensive -o -"
        " | grep -vE \"FuMa::\""
        " > {output.txt}"

rule subset_sf:
    input:
        sf="{sample}/fusion/{sample}.star-fusion",
        isect="{sample}/fusion/{sample}.fuma",
        subset_script=settings["sf_subset_script"],
    output:
        sf="{sample}/fusion/{sample}.sf-isect",
    singularity: containers["hamlet-scripts"]
    shell:
        "python3 {input.subset_script} {input.isect} {input.sf} > {output.sf} || rm -f {output.sf}"

rule plot_isect:
    input:
        txt="{sample}/fusion/{sample}.sf-isect",
    output:
        svg="{sample}/fusion/{sample}.sf-isect-circos/fsnviz.svg",
        png="{sample}/fusion/{sample}.sf-isect-circos/fsnviz.png",
    singularity: containers["fsnviz"]
    shell:
        "PERL5LIB=\"\" fsnviz --out-dir `dirname {output.svg}` --png"
        " -k human.hg38 star-fusion {input.txt}"

rule combine_plots:
    input:
        sf="{sample}/fusion/{sample}.star-fusion.svg",
        fc="{sample}/fusion/{sample}.fusioncatcher.svg",
        isect="{sample}/fusion/{sample}.sf-isect.svg",
        combine_script=settings["plot_combined_script"]
    output:
        svg="{sample}/fusion/{sample}.fusions-combined.svg",
    singularity: containers["hamlet-scripts"]
    shell:
        "python3 {input.combine_script}"
        " 'STAR-Fusion':{input.sf} 'FusionCatcher':{input.fc}"
        " 'Intersection':{input.isect} {output.svg}"
