from os import path


config["rg_stats_script"] = srcdir(path.join("scripts", "gather_rg_stats.py"))
config["sample_stats_script"] = srcdir(path.join("scripts", "gather_sample_stats.py"))

localrules: rg_stats, sample_stats

containers = {
    "crimson": "docker://quay.io/biocontainers/crimson:0.3.0--py27_1",
    "cutadapt": "docker://quay.io/biocontainers/cutadapt:1.14--py36_0",
    "debian": "docker://debian:buster-slim",
    "fastqc": "docker://quay.io/biocontainers/fastqc:0.11.8--1"
}

def get_r(strand, wildcards):
    """Get fastq files on a single strand for a sample"""
    s = config["samples"].get(wildcards.sample)
    rs = []
    for rg in sorted(s["read_groups"].keys()):
        rs.append(s["read_groups"][rg][strand])
    return rs

get_r1 = partial(get_r, "R1")
get_r2 = partial(get_r, "R2")

def get_readgroup_per_sample():
    for sample in config["samples"]:
        for rg in config["samples"][sample]["read_groups"]:
            yield rg, sample


def get_fastq(wildcards):
    """ Get the fastq files from the config """
    return (
        config["samples"][wildcards.sample]["read_groups"]
                [wildcards.read_group][wildcards.pair]
    )

def get_forward(wildcards):
    """ Get the forward fastq file from the config """
    return (
        config["samples"][wildcards.sample]["read_groups"]
                [wildcards.read_group]["R1"]
    )

def get_reverse(wildcards):
    """ Get the reverse fastq file from the config """
    return (
        config["samples"][wildcards.sample]["read_groups"]
            [wildcards.read_group]["R2"]
    )

def get_readgroup(wildcards):
    return config["samples"][wildcards.sample]["read_groups"]

rule all_qc:
    input:
        fastqc_raw = expand(
            ("{sample}/qc-seq/{read_group}/fastqc-{{pair}}-raw".format(
                sample=s, read_group=rg
            )
            for rg, s in get_readgroup_per_sample()), pair=("R1", "R2")
        ),

        cutadapt = expand(
            ("{sample}/qc-seq/{read_group}/{sample}-{read_group}-{{pair}}.fq.gz".format(
                sample=s, read_group=rg
            )
            for rg, s in get_readgroup_per_sample()),
            pair=("R1", "R2")
        ),

        fastqc_processed = expand(
            ("{sample}/qc-seq/{read_group}/fastqc-{{pair}}-processed".format(
            sample=s, read_group=rg
            )
            for rg, s in get_readgroup_per_sample()), pair=("R1", "R2")
        ),

        rg_stats_json = [
            "{sample}/qc-seq/{read_group}/stats.json".format(
                sample=s, read_group=rg) for rg, s in get_readgroup_per_sample()
        ],

        sample_stats_json = expand("{sample}/qc-seq/{sample}.seq_stats.json",
                                    sample=config["samples"]
        ),

        merged_fastqs = expand("{sample}/{sample}-{pair}.fq.gz", sample=config["samples"],
                                pair=("R1", "R2")
        ),

        merged_raw = expand("{sample}/{sample}-{pair}.raw.fq.gz", sample=config["samples"],
                                pair=("R1", "R2")
        )


rule fastqc_raw:
    """Runs FastQC for each pair of each read group of each sample given in the config file."""
    input:
        fq=get_fastq
    output:
        folder=directory("{sample}/qc-seq/{read_group}/fastqc-{pair}-raw")
    threads: 4
    conda: srcdir("envs/fastqc.yml")
    singularity: containers["fastqc"]
    shell:
        "mkdir -p {output.folder}"
        " && fastqc -o {output.folder} --extract --nogroup -f fastq "
        "--threads {threads} {input.fq}"


rule cutadapt:
    """Clip fastq files"""
    input:
        fq1=get_forward,
        fq2=get_reverse
    output:
        fq1="{sample}/qc-seq/{read_group}/{sample}-{read_group}-R1.fq.gz",
        fq2="{sample}/qc-seq/{read_group}/{sample}-{read_group}-R2.fq.gz"
    singularity: containers["cutadapt"]
    conda: "envs/cutadapt.yml"
    shell: "cutadapt -a AGATCGGAAGAG -A AGATCGGAAGAG "
           "--minimum-length=20 --quality-cutoff=20,20 "
           "--output={output.fq1} --paired-output={output.fq2} "
           "{input.fq1} {input.fq2}"


rule fastqc_processed:
    """Runs FastQC for each pair of QC-ed inputs."""
    input:
        fq1="{sample}/qc-seq/{read_group}/{sample}-{read_group}-R1.fq.gz",
        fq2="{sample}/qc-seq/{read_group}/{sample}-{read_group}-R2.fq.gz"
    output:
        folder = directory("{sample}/qc-seq/{read_group}/fastqc-{pair}-processed")
    threads: 4
    conda: srcdir("envs/fastqc.yml")
    singularity: containers["fastqc"]
    shell:
        "mkdir -p {output.folder}"
        " && fastqc -o {output.folder} --extract --nogroup -f fastq "
        "--threads {threads} {input.fq1} {input.fq2}"


rule rg_stats:
    """Gathers read statistics on the read group level."""
    input:
        raw1="{sample}/qc-seq/{read_group}/fastqc-R1-raw",
        raw2="{sample}/qc-seq/{read_group}/fastqc-R2-raw",
        proc1="{sample}/qc-seq/{read_group}/fastqc-R1-processed",
        proc2="{sample}/qc-seq/{read_group}/fastqc-R2-processed",
        rg_stats_script=config["rg_stats_script"],
    output:
        stats="{sample}/qc-seq/{read_group}/stats.json",
    conda: srcdir("envs/rg_stats.yml")
    singularity: containers["crimson"]
    shell:
        "python {input.rg_stats_script} --name {wildcards.read_group}"
        " {input.raw1} {input.raw2} {input.proc1} {input.proc2}"
        " > {output.stats}"


rule sample_stats:
    """Gathers read statistics on the sample level."""
    input:
        rg_stats=lambda wildcards:
        ("{sample}/qc-seq/{read_group}/stats.json".format(sample=wildcards.sample,
        read_group=rg) for rg in get_readgroup(wildcards)),
        sample_stats_script=config["sample_stats_script"]
    output:
        stats="{sample}/qc-seq/{sample}.seq_stats.json"
    conda: srcdir("envs/sample_stats.yml")
    singularity: containers["crimson"]
    shell:
        "python {input.sample_stats_script} --name {wildcards.sample}"
        " {input.rg_stats} > {output.stats}"


rule merge_fastqs_r1:
    """Merges all FASTQ files for a given sample from its read groups."""
    input:
        fqs = lambda wildcards: expand(
            "{sample}/qc-seq/{read_group}/{sample}-{read_group}-R1.fq.gz",
            sample=wildcards.sample, read_group=get_readgroup(wildcards)
        )
    output:
        fq="{sample}/{sample}-R1.fq.gz",
    singularity: containers["debian"]
    shell:
        "zcat {input.fqs} | gzip -c > {output.fq}"


rule merge_fastqs_r2:
    """Merges all FASTQ files for a given sample from its read groups."""
    input:
        fqs = lambda wildcards: expand(
            "{sample}/qc-seq/{read_group}/{sample}-{read_group}-R2.fq.gz",
            sample=wildcards.sample, read_group=get_readgroup(wildcards)
        )
    output:
        fq="{sample}/{sample}-R2.fq.gz",
    singularity: containers["debian"]
    shell:
        "zcat {input.fqs} | gzip -c > {output.fq}"


rule merge_fastqs_raw_r1:
    """Merges all raw FASTQ files for a given sample from its read groups."""
    input:
        fq = get_r1
    output:
        merged = temp("{sample}/{sample}-R1.raw.fq.gz")
    singularity: containers["debian"]
    shell:
        "zcat {input.fq} | gzip -c > {output.merged}"


rule merge_fastqs_raw_r2:
    """Merges all raw FASTQ files for a given sample from its read groups."""
    input:
        fq = get_r2
    output:
        merged = temp("{sample}/{sample}-R2.raw.fq.gz")
    singularity: containers["debian"]
    shell:
        "zcat {input.fq} | gzip -c > {output.merged}"
