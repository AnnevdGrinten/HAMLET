from os import path


# We need the QC-seq output
include: "../qc-seq/Snakefile"
include: "../snv-indels/Snakefile"

localrules: calc_exon_ratios, count_bases_exon, count_bases_gene

containers = {
    "bedtools-2.17-python-2.7": "docker://quay.io/biocontainers/mulled-v2-a9ddcbd438a66450297b5e0b61ac390ee9bfdb61:e60f3cfda0dfcf4a72f2091c6fa1ebe5a5400220-0",
    "htseq": "docker://quay.io/biocontainers/htseq:0.11.2--py27h637b7d7_1",
    "picard": "docker://quay.io/biocontainers/picard:2.20.5--0",
    "hamlet-scripts": "docker://quay.io/redmar_van_den_berg/hamlet-scripts:0.2"
}

settings=config["settings"]

# Set the default settings
def set_default(key, value):
    """ Set default value for settings """
    if key not in settings:
        settings[key] = value

set_default("base_count_script", srcdir(path.join("scripts", "hist2count.py")))
set_default("aggr_base_count_script", srcdir(path.join("scripts", "aggr_base_count.R")))
set_default("calc_ratio_script", srcdir(path.join("scripts", "calc_ratio.py")))
set_default("relative_gene_name", "HMBS")

if "exon_names" not in settings:
    raise ValueError("No exon names for exon ratio calculation defined")

rule all_expression:
    input:
        base_per_exon=expand("{sample}/expression/{sample}.bases_per_exon", sample=config["samples"]),
        base_per_gene=expand("{sample}/expression/{sample}.bases_per_gene", sample=config["samples"]),
        exon_ratio=expand("{sample}/expression/{sample}.exon_ratios", sample=config["samples"]),
        fragment_per_gene=expand("{sample}/expression/{sample}.fragments_per_gene", sample=config["samples"]),
        raw_base=expand("{sample}/expression/{sample}.raw_base", sample=config["samples"]),

rule idsort_aln:
    input:
        bam="{sample}/snv-indels/{sample}.snv-indel.bam",
    output:
        bam=temp("{sample}/snv-indels/{sample}.snv-indel.idsorted.bam"),
    params:
        tmp=temp("tmp"),
    threads: 1
    singularity: containers["picard"]
    shell:
        "picard -Xmx4G SortSam I={input.bam} O={output.bam} SORT_ORDER=queryname"
        " VALIDATION_STRINGENCY=SILENT CREATE_INDEX=false TMP_DIR={params.tmp}"

rule count_fragments:
    input:
        bam="{sample}/snv-indels/{sample}.snv-indel.idsorted.bam",
        gtf=settings["expression_gtf"],
    output:
        fragments_per_gene="{sample}/expression/{sample}.fragments_per_gene",
    threads: 1
    singularity: containers["htseq"]
    shell:
        "htseq-count --format bam --order name --stranded no"
        " {input.bam} {input.gtf} > {output.fragments_per_gene}"

rule count_raw_bases:
    input:
        bam="{sample}/snv-indels/{sample}.snv-indel.bam",
        bed=settings["expression_bed"],
        count_script=settings["base_count_script"],
    output:
        raw_base_count="{sample}/expression/{sample}.raw_base",
    threads: 1
    singularity: containers["bedtools-2.17-python-2.7"]
    shell:
        "bedtools coverage -split -hist -abam {input.bam} -b {input.bed}"
        " | python {input.count_script} -c 3 > {output.raw_base_count}"

rule count_bases_exon:
    input:
        raw="{sample}/expression/{sample}.raw_base",
        aggr_script=settings["aggr_base_count_script"],
    output:
        bases_per_exon="{sample}/expression/{sample}.bases_per_exon",
    params:
        sample="{sample}"
    threads: 1
    singularity: containers["hamlet-scripts"]
    shell:
        "Rscript {input.aggr_script} -I {input.raw} -N {params.sample}"
        " -E {output.bases_per_exon}"

rule count_bases_gene:
    input:
        raw="{sample}/expression/{sample}.raw_base",
        aggr_script=settings["aggr_base_count_script"],
    output:
        bases_per_gene="{sample}/expression/{sample}.bases_per_gene",
    params:
        sample="{sample}"
    threads: 1
    singularity: containers["hamlet-scripts"]
    shell:
        "Rscript {input.aggr_script} -I {input.raw} -N {params.sample}"
        " -G {output.bases_per_gene}"

rule calc_exon_ratios:
    input:
        table="{sample}/expression/{sample}.bases_per_exon",
        calc_script=settings["calc_ratio_script"],
    output:
        table="{sample}/expression/{sample}.exon_ratios",
    params:
        exon_ratio_names=" ".join(
            [settings["relative_gene_name"]] + settings.get("exon_names")
        ),
        exon_min_ratio=settings["exon_min_ratio"],
    threads: 1
    singularity: containers["hamlet-scripts"]
    shell:
        "cat {input.table}"
        " | python3 {input.calc_script} -r {params.exon_min_ratio}"
        " - {params.exon_ratio_names}"
        " > {output.table}"
