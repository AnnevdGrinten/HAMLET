include: "common.smk"


rule all:
    input:
        multiqc="multiqc_expression.html",
        seamless=[f"{sample.sample}/expression/seAMLess.tsv" for sample in samples],
        json=[module_output.json(sample) for sample in samples],


rule normalized_coverage:
    input:
        bam=get_bam,
        counts=get_counts,
        gtf=config["gtf"],
        bed=config.get("bed", []),
        src=srcdir("scripts/coverage.py"),
    params:
        housekeeping=config["housekeeping"],
        genes_of_interest=config["genes_of_interest"],
    output:
        normalized="{sample}/expression/coverage.normalized.csv",
        raw="{sample}/expression/coverage.csv",
    log:
        "log/normalized_coverage.{sample}.txt",
    threads: 1
    container:
        containers["pysam"]
    shell:
        """
        # Calculate the normalized coverage
        python3 {input.src} \
            --bam {input.bam} \
            --counts {input.counts} \
            --housekeeping {params.housekeeping} \
            --gtf {input.gtf} \
            --bed {input.bed} \
            --genes {params.genes_of_interest} \
            > {output.normalized} 2> {log}

        # Extract the raw coverage counts
        python3 {input.src} \
            --bam {input.bam} \
            --counts {input.counts} \
            --housekeeping {params.housekeeping} \
            --gtf {input.gtf} \
            --bed {input.bed} \
            --genes {params.genes_of_interest} \
            --raw \
            > {output.raw} 2>> {log}
        """


rule transform_counts:
    """Transform the counts table to use with seAMLess"""
    input:
        counts=get_counts,
        src=srcdir("scripts/transform_counts.py"),
    output:
        tsv="{sample}/expression/seAMLess.tsv",
    params:
        strandedness=get_strand,
    log:
        "log/transform_counts.{sample}.txt",
    threads: 1
    container:
        containers["pysam"]
    shell:
        """
        python3 {input.src} \
            --counts {input.counts} \
            --strand {params.strandedness} \
            --sample {wildcards.sample} \
            > {output.tsv} 2> {log}
        """


rule json_output:
    input:
        coverage=module_output.normalized_expression,
        src=srcdir("scripts/json-output.py"),
    params:
        strandedness=get_strand,
        genes=config["report"],
    output:
        "{sample}/expression/expression-output.json",
    log:
        "log/expression.{sample}.txt",
    container:
        containers["pysam"]
    shell:
        """
        python3 {input.src} \
            --coverage {input.coverage} \
            --strandedness {params.strandedness} \
            --genes {params.genes} \
            --sample {wildcards.sample} \
            > {output} 2> {log}
        """


rule merge_samples:
    input:
        counts=[module_output.normalized_expression(sample) for sample in samples],
        src=srcdir("scripts/multiqc.py"),
    params:
        samples=[sample.sample for sample in samples],
        strandedness=[get_strand(sample) for sample in samples],
    output:
        unstranded="merged_expression_unstranded_mqc.tsv",
        stranded="merged_expression_stranded_mqc.tsv",
    log:
        "log/merge_samples.txt",
    threads: 1
    container:
        containers["multiqc"]
    shell:
        """
        python3 {input.src} \
            --counts {input.counts} \
            --samples {params.samples} \
            --strandedness {params.strandedness}\
            2> {log}
        """


rule multiqc:
    input:
        stats=module_output.multiqc_files,
        config=srcdir("../../cfg/multiqc.yml"),
    params:
        filelist="multiqc_filelist_expression.txt",
        depth=2,
    output:
        html="multiqc_expression.html",
    log:
        "log/expression.multiqc.txt",
    container:
        containers["multiqc"]
    shell:
        """
        rm -f {params.filelist}

        for fname in {input.stats}; do
            echo $fname >> {params.filelist}
        done

        multiqc \
        --force \
        --dirs \
        --dirs-depth {params.depth} \
        --fullnames \
        --fn_as_s_name \
        --file-list {params.filelist} \
        --config {input.config} \
        --filename {output.html} 2> {log}
        """
