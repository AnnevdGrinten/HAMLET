from os import path

containers = {
    "bwa-0.7.17-samtools-1.3.1-picard-2.9.2": "docker://quay.io/biocontainers/mulled-v2-1c6be8ad49e4dfe8ab70558e8fb200d7b2fd7509:5900b4e68c4051137fffd99165b00e98f810acae-0",
    "rose": "docker://lumc/rose-dt:0.4"
}

settings=config["settings"]

# Set the default settings
def set_default(key, value):
    """ Set default value for settings """
    if key not in settings:
        settings[key] = value

# Region of interest ~ exon 2-10 in transcript coordinates.
set_default("kmt2a_name", "KMT2A-213")
set_default("kmt2a_start", 456)
set_default("kmt2a_end", 4719)

localrules: detect_kmt2a, plot_itd_kmt2a

rule all_kmt2a:
    input:
        kmt2a_csv=expand("{sample}/itd/{sample}.kmt2a.csv", sample=config["samples"]),
        kmt2a_bg_csv=expand("{sample}/itd/{sample}.kmt2a.bg.csv", sample=config["samples"]),
        kmt2a_sc_plots=expand("{sample}/itd/{sample}.kmt2a.png", sample=config["samples"]),

rule align_kmt2a:
    input:
        fq1="{sample}/{sample}-R1.fq.gz",
        fq2="{sample}/{sample}-R2.fq.gz",
        fasta=settings["kmt2a_fasta"]
    output:
        bam="{sample}/itd/{sample}.kmt2a.bam",
        bai="{sample}/itd/{sample}.kmt2a.bai",
    params:
        rg="@RG\\tID:{sample}\\tSM:{sample}"
    threads: 3
    singularity: containers["bwa-0.7.17-samtools-1.3.1-picard-2.9.2"]
    shell:
        "bwa mem -R \'{params.rg}\' -t {threads} -L 2,2 {input.fasta} {input.fq1} {input.fq2}"
        " | samtools view -Sbh -F 0x4 -"
        " | picard SortSam I=/dev/stdin O={output.bam} SO=coordinate CREATE_INDEX=true"

rule detect_kmt2a:
    input:
        bam="{sample}/itd/{sample}.kmt2a.bam",
        bai="{sample}/itd/{sample}.kmt2a.bai",
        ref=settings["kmt2a_fasta"],
    output:
        csv="{sample}/itd/{sample}.kmt2a.csv",
        bg_csv="{sample}/itd/{sample}.kmt2a.bg.csv",
    threads: 1
    params:
        trx_name=settings["kmt2a_name"],
        start=settings["kmt2a_start"],
        end=settings["kmt2a_end"],
    singularity: containers["rose"]
    shell:
        "rose-dt -r {params.trx_name}:{params.start}-{params.end} --bg-counts {output.bg_csv} {input.ref} {input.bam} > {output.csv}"

rule plot_itd_kmt2a:
    input:
        csv="{sample}/itd/{sample}.kmt2a.csv",
        bg_csv="{sample}/itd/{sample}.kmt2a.bg.csv",
    output:
        png="{sample}/itd/{sample}.kmt2a.png",
    threads: 1
    singularity: containers["rose"]
    shell:
        "rose-dt-plot.py"
        " --sample-id {wildcards.sample} --mode per-event"
        " {input.csv} {input.bg_csv} {output.png}"
