from os import path
from rattle import Run


RUN = Run(config)

# Region of interest ~ exon 14-15 in transcript coordinates.
RUN.set_default_setting("flt3_name", "FLT3-001")
RUN.set_default_setting("flt3_start", 1787)
RUN.set_default_setting("flt3_end", 2024)


rule all_flt3:
    input:
        flt3_sc_jsons=expand(RUN.output("{sample}/itd/{sample}.flt3-sc.json"), sample=RUN.samples),
        flt3_sc_plots=expand(RUN.output("{sample}/itd/{sample}.flt3-sc.png"), sample=RUN.samples),

rule align_flt3:
    input:
        fq1=RUN.output("{sample}/{sample}-R1.fq.gz"),
        fq2=RUN.output("{sample}/{sample}-R2.fq.gz"),
        index=RUN.settings["flt3_bwa_index"]
    output:
        bam=RUN.output("{sample}/itd/{sample}.flt3.bam"),
        bai=RUN.output("{sample}/itd/{sample}.flt3.bai"),
    params:
        rg="@RG\\tID:{sample}\\tSM:{sample}"
    threads: 3
    conda: srcdir("envs/align_itd.yml")
    shell:
        "bwa mem -R \'{params.rg}\' -t {threads} -L 2,2 {input.index} {input.fq1} {input.fq2}"
        " | samtools view -Sbh -F 0x4 -"
        " | picard SortSam I=/dev/stdin O={output.bam} SO=coordinate CREATE_INDEX=true"

rule detect_flt3:
    input:
        bam=RUN.output("{sample}/itd/{sample}.flt3.bam"),
        bai=RUN.output("{sample}/itd/{sample}.flt3.bai"),
        ref=RUN.settings["flt3_fasta"],
        exe=RUN.settings["rose_dt_exe"],
    output:
        csv=RUN.output("{sample}/itd/{sample}.flt3.csv"),
        bg_csv=RUN.output("{sample}/itd/{sample}.flt3.bg.csv"),
    threads: 1
    params:
        trx_name=RUN.settings["flt3_name"],
        start=RUN.settings["flt3_start"],
        end=RUN.settings["flt3_end"],
    shell:
        "{input.exe} -r {params.trx_name}:{params.start}-{params.end} --bg-counts {output.bg_csv} {input.ref} {input.bam} > {output.csv}"

rule plot_itd_flt3:
    input:
        csv=RUN.output("{sample}/itd/{sample}.flt3.csv"),
        bg_csv=RUN.output("{sample}/itd/{sample}.flt3.bg.csv"),
        scr=RUN.settings["plot_itd"],
    output:
        png=RUN.output("{sample}/itd/{sample}.flt3.png"),
    threads: 1
    conda: RUN.settings["plot_itd_conda"]
    shell:
        "python {input.scr}"
        " --sample-id {wildcards.sample} --mode per-event"
        " {input.csv} {input.bg_csv} {output.png}"
