include: "common.smk"


# Put each sample name in a SimpleNamespace to mimic Snakemake wildcard usage
# (e.g {wildcards.sample}). This is only used in the 'all' rule.
samples = [SimpleNamespace(sample=sample) for sample in pep.sample_table["sample_name"]]


localrules:
    table_vars_all,
    table_vars_hi,


rule all:
    input:
        bam=[module_output.bam(sample) for sample in samples],
        variant_plot_dir=[module_output.variant_plot_dir(sample) for sample in samples],
        all_csv=[module_output.var_all(sample) for sample in samples],
        high_csv=[module_output.var_hi(sample) for sample in samples],
        json=[module_output.json(sample) for sample in samples],


rule align_vars:
    input:
        fq1=get_forward,
        fq2=get_reverse,
        index=config["star_index"],
        gtf=config["gtf"],
    output:
        bam="{sample}/snv-indels/Aligned.sortedByCoord.out.bam",
        gene_count="{sample}/snv-indels/ReadsPerGene.out.tab",
    params:
        rg_sample=lambda wildcards: wildcards.sample,
        chim_segment=20,
        min_intron_size=50,
        alignInsertionFlush="Right",
    log:
        main="{sample}/snv-indels/Log.out",
        progress="{sample}/snv-indels/Log.progress.out",
        final="{sample}/snv-indels/Log.final.out",
    threads: 8
    container:
        containers["star"]
    shell:
        """
        STAR \
            --runThreadN {threads} \
            --genomeDir {input.index} \
            --sjdbGTFfile {input.gtf} \
            --readFilesCommand zcat \
            --outSAMattrRGline ID:{params.rg_sample} \
            --outFileNamePrefix $(dirname {output.bam})/ \
            --outSAMtype BAM SortedByCoordinate \
            --alignIntronMin {params.min_intron_size} \
            --alignInsertionFlush {params.alignInsertionFlush} \
            --chimOutType WithinBAM \
            --chimSegmentMin {params.chim_segment} \
            --quantMode GeneCounts \
            --readFilesIn {input.fq1:q} {input.fq2:q}
        """


rule index_bamfile:
    input:
        bam="{sample}/snv-indels/Aligned.sortedByCoord.out.bam",
    output:
        bai="{sample}/snv-indels/Aligned.sortedByCoord.out.bam.bai",
    params:
        tmp=temp("tmp"),
    log:
        "log/index_bamfile.{sample}.txt",
    container:
        containers["varscan-2.4.2-samtools-1.3.1-tabix-0.2.6-grep-2.14"]
    shell:
        """
        samtools index {input.bam} 2> {log}
        """


rule reorder_aln_header:
    input:
        bam="{sample}/snv-indels/Aligned.sortedByCoord.out.bam",
        bai="{sample}/snv-indels/Aligned.sortedByCoord.out.bam.bai",
        ref=config["genome_fasta"],
        refd=config["genome_dict"],
    output:
        bam="{sample}/snv-indels/{sample}.snv-indel.bam",
        bai="{sample}/snv-indels/{sample}.snv-indel.bai",
    params:
        rg_sample=lambda wildcards: wildcards.sample,
    log:
        "log/reorder_aln_header.{sample}.txt",
    threads: 1
    container:
        containers["picard"]
    shell:
        """
        picard -Xmx4G ReorderSam \
            I={input.bam} \
            SD={input.bam} \
            O={output.bam} \
            R={input.ref} \
            VALIDATION_STRINGENCY=SILENT \
            CREATE_INDEX=true 2> {log}
        """


rule genome_txt:
    input:
        ref_dict=config["genome_dict"],
    output:
        genome=temp(".tmp.genome.txt"),
    log:
        "log/tmp.genome.txt",
    container:
        containers["varscan-2.4.2-samtools-1.3.1-tabix-0.2.6-grep-2.14"]
    shell:
        """
        cat {input.ref_dict} \
        | grep -P "@SQ\\tSN:" \
        | sed 's/@SQ\\tSN://' \
        | sed 's/\\tLN:/\\t/' \
        | cut -f1,2 \
        > {output.genome} 2> {log}
        """


rule exon_cov_ref:
    input:
        ref_fai=config["genome_fai"],
        ref_refflat=config["annotation_refflat"],
    output:
        bed=temp(".tmp.exon_cov_ref.bed"),
    log:
        "log/exon_cov_ref.txt",
    container:
        containers["bedtools-2.27-grep-2.14-gawk-5.0-python-3.7"]
    shell:
        """
        cat {input.ref_refflat} \
         | grep -vP "chr.*(alt|random|fix)\t" \
         | awk \'{{ split($10, starts, ","); split($11, ends, ","); for (i=1; i < length(starts); i++) {{ print $3"\\t"starts[i]"\\t"ends[i]"\\t"gensub(/(\.[0-9]+)/,"", "g", $2)"\\t"i"\\t"$4 }} }}\' \
         | bedtools sort -faidx {input.ref_fai} \
         > {output.bed} 2> {log}
        """


rule exon_cov:
    input:
        bam="{sample}/snv-indels/{sample}.snv-indel.bam",
        bed=".tmp.exon_cov_ref.bed",
        genome=".tmp.genome.txt",
        idm=config["ref_id_mapping"],
        scr=config["exon_cov_script"],
    output:
        json="{sample}/snv-indels/{sample}.exon_cov_stats.json",
    log:
        "log/exon_cov.{sample}.txt",
    container:
        containers["bedtools-2.27-grep-2.14-gawk-5.0-python-3.7"]
    shell:
        """
        bedtools coverage \
            -d \
            -sorted \
            -g {input.genome} \
            -a {input.bed} \
            -b {input.bam} 2> {log} \
        | cut -f1,2,3,4,5,8,7 \
        | python {input.scr} \
            --id-mapping {input.idm} - {output.json} 2> {log}
        """


rule call_vars:
    input:
        bam="{sample}/snv-indels/{sample}.snv-indel.bam",
        ref=config["genome_fasta"],
    output:
        vcf="{sample}/snv-indels/{sample}.raw.vcf.gz",
    log:
        "log/call_vars.{sample}.txt",
    threads: 3
    container:
        containers["varscan-2.4.2-samtools-1.3.1-tabix-0.2.6-grep-2.14"]
    shell:
        """
        samtools mpileup \
            -f {input.ref} \
            -d 1000000 \
            -s \
            -B {input.bam} \
        | grep -vP '\\t\\t' \
        | varscan mpileup2cns \
            --strand-filter 0 \
            --output-vcf 1 \
            --min-var-freq 0.1 \
            --p-value 0.05 2> {log} \
        | grep -vP '\\t\./\.|\\t0/0' \
        | bgzip -c > {output.vcf}
        """


rule aln_stats:
    input:
        bam="{sample}/snv-indels/{sample}.snv-indel.bam",
        bai="{sample}/snv-indels/{sample}.snv-indel.bai",
        ref=config["genome_fasta"],
        ref_dict=config["genome_dict"],
    output:
        stats="{sample}/snv-indels/{sample}.aln_stats",
    log:
        "log/aln_stats.{sample}.txt",
    threads: 1
    container:
        containers["picard"]
    shell:
        """
        picard -Xmx4G CollectAlignmentSummaryMetrics \
            VALIDATION_STRINGENCY=LENIENT \
            R={input.ref} \
            I={input.bam} \
            O={output.stats} 2> {log}
        """


rule insert_stats:
    input:
        bam="{sample}/snv-indels/{sample}.snv-indel.bam",
        bai="{sample}/snv-indels/{sample}.snv-indel.bai",
        ref=config["genome_fasta"],
        ref_dict=config["genome_dict"],
    output:
        stats="{sample}/snv-indels/{sample}.insert_stats",
        histo="{sample}/snv-indels/{sample}.insert_stats.pdf",
    log:
        "log/insert_stats.{sample}.txt",
    threads: 1
    container:
        containers["picard"]
    shell:
        """
        picard -Xmx4G CollectInsertSizeMetrics \
            VALIDATION_STRINGENCY=LENIENT \
            R={input.ref} \
            I={input.bam} \
            O={output.stats} \
            H={output.histo} 2> {log}
        """


rule rna_stats:
    input:
        bam="{sample}/snv-indels/{sample}.snv-indel.bam",
        bai="{sample}/snv-indels/{sample}.snv-indel.bai",
        ref=config["genome_fasta"],
        ref_dict=config["genome_dict"],
        ref_rrna=config["rrna_refflat"],
        annot=config["annotation_refflat"],
    output:
        stats="{sample}/snv-indels/{sample}.rna_stats",
        histo="{sample}/snv-indels/{sample}.rna_stats.pdf",
    log:
        "log/rna_stats.{sample}.txt",
    threads: 1
    container:
        containers["picard"]
    shell:
        """
        picard -Xmx4G CollectRnaSeqMetrics \
             VALIDATION_STRINGENCY=LENIENT \
             R={input.ref} \
             REF_FLAT={input.annot} \
             RIBOSOMAL_INTERVALS={input.ref_rrna} \
             STRAND_SPECIFICITY=NONE \
             ASSUME_SORTED=true \
             CHART_OUTPUT={output.histo} \
             I={input.bam} \
             O={output.stats} 2> {log}
        """


rule annotate_vars:
    input:
        vcf="{sample}/snv-indels/{sample}.raw.vcf.gz",
        ref_1kg=config["vcf_1kg"],
        ref_gonl=config["vcf_gonl"],
        genome_fasta=config["genome_fasta"],
    params:
        online="" if config.get("cache_vep") else "--database",
        offline=f" --offline --cache_version 108 --everything --merged --dir {config['cache_vep']}"
        if config.get("cache_vep")
        else "",
    output:
        vcf="{sample}/snv-indels/{sample}.annotated.vcf.gz",
        stats="{sample}/snv-indels/{sample}.vep_stats.txt",
    log:
        "log/annotate_vars.{sample}.txt",
    threads: 8
    container:
        containers["vep"]
    shell:
        """
        vep \
            -i {input.vcf} \
            --fasta {input.genome_fasta} \
            {params.online} \
            {params.offline} \
            --custom {input.ref_1kg},P3,vcf,exact,0,AF,AFR_AF,AMR_AF,EAS_AF,EUR_AF,SAS_AF \
            --custom {input.ref_gonl},GONL,vcf,exact,0,AF \
            --fork {threads} \
            --allele_number --stats_text --vcf --force_overwrite --assembly GRCh38 \
            --format vcf \
            --polyphen b \
            --sift b \
            --hgvs \
            --stats_file {output.stats} -o STDOUT \
            2> {log} | bgzip -c > {output.vcf}
        """


rule extract_vars:
    input:
        vcf="{sample}/snv-indels/{sample}.annotated.vcf.gz",
        ref_hotspots=config["bed_variant_hotspots"],
        ref_id_mapping=config["ref_id_mapping"],
        scr=config["extract_script"],
    output:
        json="{sample}/snv-indels/{sample}.variants.json",
    log:
        "log/extract_vars.{sample}.txt",
    threads: 1
    container:
        containers["hamlet-scripts"]
    shell:
        """
        python3 {input.scr} \
            --hotspots {input.ref_hotspots} \
            --sample-id {wildcards.sample} \
            {input.ref_id_mapping} \
            {input.vcf} > {output.json} 2> {log}
        """


rule table_vars_all:
    input:
        json="{sample}/snv-indels/{sample}.variants.json",
        scr=config["csv_script"],
    output:
        csv="{sample}/snv-indels/{sample}.variants_all.csv",
    log:
        "log/table_vars_all.{sample}.txt",
    threads: 1
    container:
        containers["hamlet-scripts"]
    shell:
        """
        python3 {input.scr} {input.json} > {output.csv} 2> {log}
        """


rule table_vars_hi:
    input:
        json="{sample}/snv-indels/{sample}.variants.json",
        scr=config["csv_script"],
    output:
        csv="{sample}/snv-indels/{sample}.variants_hi.csv",
    log:
        "log/table_vars_hi.{sample}.txt",
    threads: 1
    container:
        containers["hamlet-scripts"]
    shell:
        """
        python3 {input.scr} --hi {input.json} > {output.csv} 2> {log}
        """


rule plot_vars:
    input:
        json="{sample}/snv-indels/{sample}.variants.json",
        scr=config["plot_script"],
        ref_id_mapping=config["ref_id_mapping"],
        ref_hotspots=config["bed_variant_hotspots"],
        gtf=config["gtf"],
    output:
        plotd=directory("{sample}/snv-indels/variant_plots"),
    log:
        "log/plot_vars.{sample}.txt",
    threads: 1
    container:
        containers["hamlet-scripts"]
    shell:
        """
        mkdir -p {output.plotd} && \
        Rscript {input.scr} \
            --input-json {input.json} \
            --input-gene-ids {input.ref_id_mapping} \
            --input-gtf {input.gtf} \
            --input-hotspot-bed {input.ref_hotspots} \
            --out-dir `dirname {output.plotd}` 2> {log}
        """


rule json_output:
    input:
        id_mappings_path=config["ref_id_mapping"],
        var_plot_dir="{sample}/snv-indels/variant_plots",
        var_csv="{sample}/snv-indels/{sample}.variants_hi.csv",
        aln_stats="{sample}/snv-indels/{sample}.aln_stats",
        rna_stats="{sample}/snv-indels/{sample}.rna_stats",
        insert_stats="{sample}/snv-indels/{sample}.insert_stats",
        exon_cov_stats="{sample}/snv-indels/{sample}.exon_cov_stats.json",
        vep_stats="{sample}/snv-indels/{sample}.vep_stats.txt",
        src=srcdir("scripts/json-output.py"),
    output:
        "{sample}/snv-indels/snv-indels-output.json",
    log:
        "log/snv_indels_output.{sample}.txt",
    container:
        containers["crimson"]
    shell:
        """
        python3 {input.src} \
            {input.id_mappings_path} \
            {input.var_plot_dir} \
            {input.var_csv} \
            {input.aln_stats} \
            {input.rna_stats} \
            {input.insert_stats} \
            {input.exon_cov_stats} \
            {input.vep_stats} > {output} 2> {log}
        """
